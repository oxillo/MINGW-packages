# Maintainer: Olivier XILLO 

_realname=libed247
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.0
pkgrel=1
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
pkgdesc="Airbus implementation of ED-247 standard"
depends=("${MINGW_PACKAGE_PREFIX}-libxml2")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-doxygen"
             "git")
checkdepends=("${MINGW_PACKAGE_PREFIX}-gtest")
license=("spdx:MIT")
url="https://github.com/airbus/ED247_LIBRARY.git"
_commit="3aea10b83661cb4418e6c9ba6b052ffca7868335"
source=(${_realname}::"git+${url}#commit=${_commit}"
        "0001-fix-include-limits.patch"
        "0002-fix-tests-compilation.patch"
        )
sha256sums=('0ad9fa0fd011ae78316e1b493027ba2153870495dfb4a8a9c38fb43c7b79bf2c'
            'db0695963610a0d5be1b08c732c5e66662ee21ac08fbbff84c8ad9203c09c56c'
            'f99acc9203db32bffe91bc4dce1209afa50c428762409c6912b1065bd3fa3ed4')

prepare() {
  cd "${srcdir}"/${_realname}

  git apply "${srcdir}/0001-fix-include-limits.patch"
  git apply "${srcdir}/0002-fix-tests-compilation.patch"
}

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    "${extra_config[@]}" \
    -S "${_realname}" \
    -B "build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/cmake.exe --build "build-${MSYSTEM}" --target ed247
}

check() {
  ${MINGW_PREFIX}/bin/cmake.exe --build "build-${MSYSTEM}" --target run_tests
}

package() {
  
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install "build-${MSYSTEM}"
  # install licenses
  install -Dm644 "${srcdir}/${_realname}/LICENSE.md" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.md"
}
